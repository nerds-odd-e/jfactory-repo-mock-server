buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.5.2'
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'de.marcphilipp.nexus-publish' version '0.2.0'
}

jacoco {
    toolVersion = "0.8.2"
}

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'info.solidsoft.pitest'
apply plugin: "maven"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

group = 'com.yaoruozhou'
version = '0.1.0'
sourceCompatibility = 1.8

jacocoTestReport {
    reports {
        xml.enabled = true // for coveralls
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.leeonky:jfactory:0.1.0'
    implementation 'com.github.leeonky:jfactory-cucumber:0.1.3'
    implementation 'org.mock-server:mockserver-client-java:5.11.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    implementation("io.cucumber:cucumber-java:6.11.0")
    implementation 'com.github.leeonky:bean-util:0.2.2'

    testImplementation("io.cucumber:cucumber-spring:6.11.0")
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.4.4'
    testImplementation("com.squareup.okhttp3:okhttp:4.9.0")
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.3'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.mock-server:mockserver-netty:5.11.2'
    testImplementation 'org.mockito:mockito-core:3.12.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.3'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    pitest 'io.github.wmaarts:pitest-mutation-testing-elements-plugin:0.3.1'
}

test {
    useJUnitPlatform()
}

pitest {
    targetClasses = ['com.yaoruozhou']
    outputFormats = ['HTML2']
    junit5PluginVersion = "0.12"
}

build.dependsOn 'pitest'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.name
                url = 'https://github.com/nerds-odd-e/' + project.name

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'josephyao'
                        name = 'Joseph Yao'
                        email = 'joseph@odd-e.com'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/nerds-odd-e/' + project.name + '.git'
                    developerConnection = 'scm:git:git@github.com:nerds-odd-e' + project.name + '.git'
                    url = 'https://github.com/nerds-odd-e/' + project.name + '.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    username = System.env.ossrhUsername
    password = System.env.ossrhPassword
}

task currentVersion {
    doLast {
        println project.version
    }
}

tasks.coveralls {
    dependsOn jacocoTestReport
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber(type: JavaExec) {
    dependsOn assemble, testClasses
    main = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['--plugin', 'pretty',
            '--glue', 'com.yaoruozhou.jfactory.cucumber',
            '--glue', 'com.github.leeonky.jfactory.cucumber',
            'src/test/resources']
}